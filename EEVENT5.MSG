
;#103	マジカントへ	13.13

;★８つのパワースポットのサウンドをすべて集めるとマジカントが出現する。
;(マジカントへ次元スリップする。)
;★このとき、どこが８つめのパワースポットになっても、共通の演出で、
;ジャイアントステップからファイアスプリングまで８つのパワースポットを
;順々に巡って行くシーンが挿入される。
;(つまり、どのパワースポットをトリガーとしても、
;マジカントへは必ずファイアスプリングから行くこととなる。)
;★ネス以外のキャラはファイアスプリングでネスの(意識が)戻るのを待つ
;という設定。
;★演出の詳細は未定。

MSG_CHK_ALLSOUND;
@CHKF(FLG_WIN_OSCAR)
@FALSE_GOTO(_CHK_ALLSOUND_2)
@!
_CHK_ALLSOUND_2;
@CHKFGOTO(FLG_POWR_GIAN,_ALLSOUND_1)
@!
_ALLSOUND_1;
@CHKFGOTO(FLG_POWR_LLPT,_ALLSOUND_2)
@!
_ALLSOUND_2;
@CHKFGOTO(FLG_POWR_MLKY,_ALLSOUND_3)
@!
_ALLSOUND_3;
@CHKFGOTO(FLG_POWR_MGNT,_ALLSOUND_4)
@!
_ALLSOUND_4;
@CHKFGOTO(FLG_POWR_PINK,_ALLSOUND_5)
@!
_ALLSOUND_5;
@CHKFGOTO(FLG_POWR_RAIN,_ALLSOUND_6)
@!
_ALLSOUND_6;
@CHKFGOTO(FLG_POWR_LUMI,_ALLSOUND_7)
@!
_ALLSOUND_7;
@CHKFGOTO(FLG_POWR_FIRE,_ALLSOUND_OK)
@!
_ALLSOUND_OK;
@SETF(FLG_POWR_ALL)
@FUNC(6)		;マップ上での全滅許可
_ALLSOUND_2ND;
_POWR_OK_2;
@DEL_PARTY(PC_PAULA)
@DEL_PARTY(PC_JEFF)
@DEL_PARTY(PC_POO)

;自己意識内マイホームでの目覚め
;-----
@GOSUB(MSG_GLOBAL_DELIVERY_RESET)	;場面転換時に配達を中止
;@CLRF(FLG_SYS_DISTLPT)			;テレポート禁止解除
@SETF(FLG_SYS_MONSTER_OFF)	;ワンダリングモンスターを出現させない
@GOSUB(MSG_SUB_TLPTOK_OTHERDELIVERY)
@FUNC(5)		;マップ上での全滅・配達禁止
;-----
;サウンド確認画面スルー
@BGM_EFFECT( 3 )	;BGMフェードアウト長め
@MOVE_GOM_CHAR( GOM_HIT_CHR,ANIM_INTO_NIGHT,OBJFX_SHOW_NONE )	;フェードアウト
@WAITSYS()
@HIDE_PLAYER(PC_ALL,OBJFX_SHOW_NONE)
@WARP( WARP_PUBLIC_BLACKSCREEN );真っ暗画面へワープ(無音)
@WI( 30 )
@FUNC(16);自動演奏自動終了
@WI( 30 )
;-----
;モノクロネスが歩む
@MOVE_GOM_CHAR( GOM_NES_MO, ANIM_EVT_INTO_PSYCHO_MYHOME, OBJFX_AFTER_WARP )
@WARP( WARP_PSYCHO_MYHOME_ROAD )	;意識内マイホームへの道へ
;WI(1)
@SET_CAMERA_GOM_CHAR( GOM_NES_MO )
@FRESH_PLAYER( PC_ALL )
@WAITSYS()
@SET_CAMERA_PARTY()
@COLD_PLAYER( PC_ALL )
@HIDE_GOM_CHAR( GOM_NES_MO, OBJFX_HIDE_DOTS )	;モノクロネス消す
@WI( 180 )		;OBJFX効果の間

;自己意識内マイホーム１階
@WARP( WARP_PSYCHO_MYHOME_1F_E )
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_EVT_PSYCHO_MYHOME_1F, OBJFX_SHOW_NONE )
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )

;自己意識内マイホーム２階廊下
@WARP( WARP_PSYCHO_MYHOME_PASS_W )
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_EVT_PSYCHO_MYHOME_ROUKA, OBJFX_SHOW_NONE )
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )

;自己意識内マイホーム２階ネスの部屋
@WARP( WARP_PSYCHO_MYHOME_NES_W )
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_EVT_PSYCHO_MYROOM, OBJFX_SHOW_NONE )
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )
@WI( 120 )
;パパママのセリフ
@OPEN(1)
◆@DSP_NAME(PC_NESS)...Hmmm...@KEY()↓
◆I think @DSP_NAME(PC_NESS) is the right name for him.
;jpn:◆@DSP_STS(STS_PC_NAME_NES)‥‥@WI(30)↓
;jpn: うーん やっぱり↓
;jpn: @DSP_STS(STS_PC_NAME_NES)ってなまえで いいか。
@KEY()
@CLOSEALL()
@WI( 60 )
@OPEN(1)
◆@DSP_NAME(PC_NESS)...he smiled just from hearing his own name.@KEY()↓
◆Do you think he likes his name?
;jpn:◆@DSP_STS(STS_PC_NAME_NES)‥‥@WI(30)このあかちゃん↓
;jpn: じぶんのなまえを きいて↓
;jpn: わらったわよ。@KEY()↓
;jpn:◆きに いってるのかしらね。
@KEY()
@CLOSEALL()
@WI( 60 )
@WI( 60 )
@OPEN(1)
◆Try putting that red cap on him.@KEY()↓
◆Ha ha ha!↓
  It’s too big, but it looks good on him...
;jpn:◆ちょっと その‥‥↓
;jpn: あかい やきゅうぼうを↓
;jpn: かぶらせて ごらんよ。@KEY()↓
;jpn:◆アッハッハ。@WI(20)↓
;jpn: ぶかぶかだけど けっこう↓
;jpn: にあうじゃないか。
@KEY()
@CLOSEALL()
@WI( 60 )
@OPEN(1)
◆I hope @DSP_STS(STS_PARTY_DOG_NAME) won’t be jealous of the baby.
;jpn:◆@DSP_STS(STS_PARTY_DOG_NAME)が あかちゃんに↓
;jpn: やきもちやかないかしらね。
@KEY()
@CLOSEALL()
@WI( 60 )
@WI( 60 )
@OPEN(1)
◆Let’s celebrate with some @DSP_STS(STS_PARTY_FAVORITE_FOODS).@KEY()↓
◆This baby will grow up to be a hard worker just like you.
;jpn:◆@DSP_STS(STS_PARTY_FAVORITE_FOODS)で↓
;jpn: おいわいを しよう。@KEY()↓
;jpn:◆このこも おまえににて↓
;jpn: ガンバリやになるんだろうなぁ。
@KEY()
@CLOSEALL()
@WI( 60 )
@OPEN(1)
◆I don’t think he needs to be rich or famous...@KEY()↓
◆but I want him to be a thoughtful, strong boy.
;jpn:◆えらいひとや おかねもちに↓
;jpn: ならなくてもいいけど‥‥@KEY()↓
;jpn:◆おもいやりのある↓
;jpn: つよいこに そだってほしいわ。
@KEY()↓
@CLOSEALL()
@WI( 60 )
@WI( 60 )
@WI( 60 )
@OPEN(1)
◆That’s odd...the baby bottle that he pointed at seemed to move a little bit...@KEY()↓
◆Hmmm...
;jpn:◆あら@WI(30) このこが↓
;jpn: ゆびさした ほにゅうびん↓
;jpn: ちょっと うごいたみたいよ。@KEY()↓
;jpn:◆‥‥ふしぎだわ。
@KEYNP()@WI(15)
@CLOSEALL()
@WI(60)
@GOSUB( MSG_SUB_BGM_FADEOUT_2SECOND )
@SETF( FLG_BGM_MGKT_IN )		;マジカント突入BGMキー
@MUSISTART()
@WI(120)
@WI(120)
@WI(120)
;------------
@FRESH_PLAYER( PC_ALL )
;マジカントホワイトフェードワープ
@MOVE_GOM_CHAR( GOM_NES_NUDE, ANIM_SEAL_PC_ALL_DIRDOWN, OBJFX_AFTER_WARP )
@WARP( WARP_MGKT_START )
@HIDE_GOM_CHAR( GOM_NES_NUDE, OBJFX_HIDE_NONE )
@SHOW_PLAYER( PC_ALL, OBJFX_SHOW_NONE )
@SET_COMEBACK_POS(TLPT_ONETT)	;グッズテレポート禁止が前提
@GOSUB( MSG_SUB_BGM_FADEOUT_LONG )
@RESF( FLG_BGM_MGKT_IN )
@MUSISTART()
@FUNC(6)		;マップ上での全滅・配達許可
@CLRF(FLG_SYS_MONSTER_OFF)	;ワンダリングモンスターを出現させる
』


;----------------------------------------

MSG_FOLLOW_GOM_HIT_CHR_SUBR;
;透明君についてゆくときの「お決まりフレーズ」サブルーチン
;  @MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_xxxx, OBJFX_SHOW_NONE )してから呼ぶ
@WI(1)			; GOM_HIT_CHRの登録待ち
@SET_CAMERA_GOM_CHAR( GOM_HIT_CHR )
@FRESH_PLAYER( PC_ALL )
@WAITSYS()
@SET_CAMERA_PARTY()
@COLD_PLAYER( PC_ALL )
』

;----------------------------------------
;#104	●フライングマン	14.1	T
;----------------------------------------


;#105	フライングマンの墓	14.2

;★墓石のオブジェクトのシナリオフラグ FLG_SYS_FLYINGMAN_DEAD_A〜E までを、
;それぞれフライングマン１〜５が死んだ時に ON にしたい。
;次のフライングマンを連れているいないにかかわらず、死んだら墓を出現させる。

;---------------------------------------------------------------------------


;#106	マジカントを出る	14.3

;★黄金のオスカーを倒すと付近に見えないキャラが出現する。
;ネスが近づいたらメッセージ MSG_MGKT_MYSTICMAN を発行しマジカント篇終了。
;夢から覚めるような演出。

;★場面変わって、ファイアスプリングのパワースポット。
;横たわったネスをポーラ、ジェフ、プーが取り囲んで
;ネスの意識の戻るのを待っている。
;★起き上がるネスに、ポーラ、ジェフ、プーの順に話し掛け
;(MSG_DKFK_POLA_NES、MSG_DKFD_JEFF_NES、MSG_DKFD_PU_NES)
;プーのセリフでサターンバレーにテレポート(オートイベント)。

;初回は黄金のオスカーを倒す、２回目は包囲網で以下
MSG_EVT_106_MAGICANT_OUT;
@FUNC(5)			;マップ上での全滅・配達禁止
@SETF(FLG_SYS_MONSTER_OFF)	;ワンダリングモンスターを出現させない
@WI(60)
MSG_MGKT_MYSTICMAN;ふしぎな人
;黄金のオスカーを倒した後(マジカント訪問２回目以降も)
;オスカー像のあったあたりでナレーションとして聞こえる。
@GOSUB( MSG_SUB_BGM_FADEOUT_LONG )
@WI( 120 )
@OPEN(1)
◆Now you can wake up.↓
  Your friends are waiting for you.@KEY()↓
◆It is time to get up.
;jpn:◆さぁ めを さましても いいぞ。↓
;jpn: なかまが まっている。↓
;jpn: さぁ めを さますんだ。
@KEY()
@CLOSEALL()
@WI(1)
;-----
@GOSUB(_EVT_106_DEL_FLYINGMAN)
@GOSUB(MSG_GLOBAL_DELIVERY_RESET)	;場面転換時に配達を中止
;-----
@BGMSTART(0,BGM_fmagi);マジカントから帰る音
@REMOVE_GOM_CHAR( GOM_NES, ANIM_KAITEN_NESS )     ;回転ネス
@WI(1)
@SET_CAMERA_GOM_CHAR( GOM_NES )                           ;頂上へ

@FRESH_PLAYER(PC_ALL)
@WAITSYS()
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_BG_CHANGE,OBJFX_SHOW_NONE)   ;背景の切替え
@WAITSYS()
@SET_CAMERA_PARTY()
@COLD_PLAYER(PC_ALL)
@SETF(FLG_FIRE_PARTY_APPEAR)
@GOSUB( MSG_SUB_BGM_FADEOUT_LONG )

;;最後にサウンドを手に入れた場所がどこかを調べる
@CHKFGOTO(FLG_MGKT_BACK_FIRE,_MGKT_BACK_FIRE)
@CHKFGOTO(FLG_MGKT_BACK_MGNT,_MGKT_BACK_MGNT)
@CHKFGOTO(FLG_MGKT_BACK_MLKY,_MGKT_BACK_MLKY)

_MGKT_BACK_LLPT;リリパットステップに戻る
@WARP(WARP_MGKT_BACK_LLPT)
@COLD_PLAYER(PC_ALL)
;-----
@WI(60)
@GOSUB(MSG_MGKT_SOUNDSTONE_BROKE);音の石消える
@WI(60)
;-----
;@WI(120)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_A,ANIM_NESS_AWAKE)
@WI(30)
@SHOW_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_B,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_C,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_D,MANPU_BIKKURI)
@WI(90)
@HIDE_CHAR(PRSN_GRFD_LILIPUT_2_A,OBJFX_HIDE_NONE)         ;倒れたネスを消す
@SHOW_PLAYER(PC_NESS,OBJFX_SHOW_NONE)                ;プレーヤーネス再表示
@WI(30)
@HIDE_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_B)
@HIDE_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_C)
@HIDE_MANPU_CHAR(PRSN_GRFD_LILIPUT_2_D)
@WI(90)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_B,ANIM_TALK_PAULA)
@GOSUB(MSG_DKFD_PAULA_NES)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_C,ANIM_TALK_JEFF)
@GOSUB(MSG_DKFD_JEFF_NES)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_D,ANIM_TALK_POO)
@GOSUB(MSG_DKFD_POO_NES)
@ADD_PARTY(PC_PAULA)
@HIDE_PLAYER(PC_PAULA,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_B,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GRFD_LILIPUT_2_B,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_PAULA,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_JEFF)
@HIDE_PLAYER(PC_JEFF,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_C,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GRFD_LILIPUT_2_C,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_JEFF,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_POO)
@HIDE_PLAYER(PC_POO,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GRFD_LILIPUT_2_D,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GRFD_LILIPUT_2_D,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_POO,OBJFX_SHOW_NONE)
;
;テストです(松井)
_MGKT_BACK_GO_DOSEI;
;@SETF(FLG_BGM_TELEPORT)
;@MUSISTART()
@WI(120)
@TELEPORT(TLPT_DOSEIDANI,TLPTFX_EVENT);土星さん谷へテレポート
@WI(120)
;@CLRF(FLG_BGM_TELEPORT)
;@MUSISTART()
;
@SET_COMEBACK_POS(TLPT_DOSEIDANI)
@CLRF(FLG_FIRE_PARTY_APPEAR)
@SETF(FLG_DKFD_DOSEI_ST1_APPEAR)
@CLRF(FLG_INSEKI_STOPPER_APPEAR);オネットの車止めがようやく消える
;-----
@FRESH_PLAYER(PC_ALL)
@FUNC(6)			;マップ上での全滅・配達許可
@RESF(FLG_SYS_MONSTER_OFF)	;ワンダリングモンスターを出現させる
@!

_MGKT_BACK_MLKY;ミルキーウェルに戻る
@WARP(WARP_MGKT_BACK_MLKY)
@COLD_PLAYER(PC_ALL)
;-----
@WI(60)
@GOSUB(MSG_MGKT_SOUNDSTONE_BROKE);音の石消える
@WI(60)
;-----
;@WI(120)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_A,ANIM_NESS_AWAKE)
@WI(30)
@SHOW_MANPU_CHAR(PRSN_GPFT_MILKY_2_B,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_GPFT_MILKY_2_C,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_GPFT_MILKY_2_D,MANPU_BIKKURI)
@WI(90)
@HIDE_CHAR(PRSN_GPFT_MILKY_2_A,OBJFX_HIDE_NONE)         ;倒れたネスを消す
@SHOW_PLAYER(PC_NESS,OBJFX_SHOW_NONE)                ;プレーヤーネス再表示
@WI(30)
@HIDE_MANPU_CHAR(PRSN_GPFT_MILKY_2_B)
@HIDE_MANPU_CHAR(PRSN_GPFT_MILKY_2_C)
@HIDE_MANPU_CHAR(PRSN_GPFT_MILKY_2_D)
@WI(90)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_B,ANIM_TALK_PAULA)
@GOSUB(MSG_DKFD_PAULA_NES)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_C,ANIM_TALK_JEFF)
@GOSUB(MSG_DKFD_JEFF_NES)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_D,ANIM_TALK_POO)
@GOSUB(MSG_DKFD_POO_NES)
@ADD_PARTY(PC_PAULA)
@HIDE_PLAYER(PC_PAULA,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_B,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GPFT_MILKY_2_B,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_PAULA,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_JEFF)
@HIDE_PLAYER(PC_JEFF,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_C,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GPFT_MILKY_2_C,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_JEFF,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_POO)
@HIDE_PLAYER(PC_POO,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_GPFT_MILKY_2_D,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_GPFT_MILKY_2_D,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_POO,OBJFX_SHOW_NONE)
;
@GOSUB(_MGKT_BACK_GO_DOSEI)
@!

_MGKT_BACK_MGNT;ピンククラウドに戻る
@WARP(WARP_MGKT_BACK_PINK)
@COLD_PLAYER(PC_ALL)
;-----
@WI(60)
@GOSUB(MSG_MGKT_SOUNDSTONE_BROKE);音の石消える
@WI(60)
;-----
;@WI(120)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_A,ANIM_NESS_AWAKE)
@WI(30)
@SHOW_MANPU_CHAR(PRSN_RAMA_PINK_5_B,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_RAMA_PINK_5_C,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_RAMA_PINK_5_D,MANPU_BIKKURI)
@WI(90)
@HIDE_CHAR(PRSN_RAMA_PINK_5_A,OBJFX_HIDE_NONE)         ;倒れたネスを消す
@SHOW_PLAYER(PC_NESS,OBJFX_SHOW_NONE)                ;プレーヤーネス再表示
@WI(30)
@HIDE_MANPU_CHAR(PRSN_RAMA_PINK_5_B)
@HIDE_MANPU_CHAR(PRSN_RAMA_PINK_5_C)
@HIDE_MANPU_CHAR(PRSN_RAMA_PINK_5_D)
@WI(90)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_B,ANIM_TALK_PAULA)
@GOSUB(MSG_DKFD_PAULA_NES)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_C,ANIM_TALK_JEFF)
@GOSUB(MSG_DKFD_JEFF_NES)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_D,ANIM_TALK_POO)
@GOSUB(MSG_DKFD_POO_NES)
@ADD_PARTY(PC_PAULA)
@HIDE_PLAYER(PC_PAULA,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_B,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_RAMA_PINK_5_B,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_PAULA,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_JEFF)
@HIDE_PLAYER(PC_JEFF,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_C,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_RAMA_PINK_5_C,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_JEFF,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_POO)
@HIDE_PLAYER(PC_POO,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_RAMA_PINK_5_D,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_RAMA_PINK_5_D,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_POO,OBJFX_SHOW_NONE)
;
@GOSUB(_MGKT_BACK_GO_DOSEI)
@!

;
_MGKT_BACK_FIRE;ファイアスプリングに戻る
@WARP(WARP_FIRE_PARTY)
@COLD_PLAYER(PC_ALL)
;-----
@WI(60)
@GOSUB(MSG_MGKT_SOUNDSTONE_BROKE);音の石消える
@WI(60)
;-----
;@WI(120)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_A,ANIM_NESS_AWAKE)
@WI(30)
@SHOW_MANPU_CHAR(PRSN_CHKA_FIRE_9_B,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_CHKA_FIRE_9_C,MANPU_BIKKURI)
@SHOW_MANPU_CHAR(PRSN_CHKA_FIRE_9_D,MANPU_BIKKURI)
@WI(90)
@HIDE_CHAR(PRSN_CHKA_FIRE_9_A,OBJFX_HIDE_NONE)         ;倒れたネスを消す
@SHOW_PLAYER(PC_NESS,OBJFX_SHOW_NONE)                ;プレーヤーネス再表示
@WI(30)
@HIDE_MANPU_CHAR(PRSN_CHKA_FIRE_9_B)
@HIDE_MANPU_CHAR(PRSN_CHKA_FIRE_9_C)
@HIDE_MANPU_CHAR(PRSN_CHKA_FIRE_9_D)
@WI(90)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_B,ANIM_TALK_PAULA)
@GOSUB(MSG_DKFD_PAULA_NES)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_C,ANIM_TALK_JEFF)
@GOSUB(MSG_DKFD_JEFF_NES)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_D,ANIM_TALK_POO)
@GOSUB(MSG_DKFD_POO_NES)
@ADD_PARTY(PC_PAULA)
@HIDE_PLAYER(PC_PAULA,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_B,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_CHKA_FIRE_9_B,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_PAULA,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_JEFF)
@HIDE_PLAYER(PC_JEFF,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_C,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_CHKA_FIRE_9_C,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_JEFF,OBJFX_SHOW_NONE)
;
@ADD_PARTY(PC_POO)
@HIDE_PLAYER(PC_POO,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_CHKA_FIRE_9_D,ANIM_ADDPARTY)
@WAITSYS()
@HIDE_CHAR(PRSN_CHKA_FIRE_9_D,OBJFX_HIDE_NONE)
@SHOW_PLAYER(PC_POO,OBJFX_SHOW_NONE)
;
@GOSUB(_MGKT_BACK_GO_DOSEI)
@!


MSG_DKFD_PAULA_NES;倒れているネスを囲んでオートでしゃべりだす
;ポーラ
@WAITSYS()
@OPEN(1)
◆What happened, @DSP_NAME(PC_NESS)?@KEY()↓
◆You’ve been unconscious for a long time...
;jpn:◆どうしてしまったの @DSP_STS(STS_PC_NAME_NES)!↓
;jpn: ずっと きを うしなってたのよ。
@KEY()
@CLOSEALL()
@WI(30)
@!

MSG_DKFD_JEFF_NES;
;ジェフ
@WAITSYS()
@OPEN(1)
◆You kept saying something...
;jpn:◆ずっと うわごとを いってたよ。
@KEY()
@CLOSEALL()
@WI(30)
@!

MSG_DKFD_POO_NES;
;プー
@WAITSYS()
@OPEN(1)
◆...Saturn Valley?↓
  What’s waiting for us there?@KEY()↓
◆Anyway, we need to teleport...
;jpn:◆‥‥サターンバレー?!↓
;jpn: なにがまっているのか‥‥↓
;jpn: とにかくテレポーテーションだ!
@KEY()
@CLOSEALL()
@WI(30)
@!


@WARP(WARP_MGKT_BACK_LLPT)

_EVT_106_DEL_FLYINGMAN;
@GET_ORDER_PLAYER(2)
@FALSE_GOTO(_EVT_106_FMAN_END)
@SET_REG(0)
@SAVE_REG()
@EQ(PC_NUIGURUMI)
@TRUE_GOTO(_EVT_106_FMAN_END)
@LOAD_REG()
@EQ(PC_NUIGURUMI_SUPER)
@TRUE_GOTO(_EVT_106_FMAN_END)
@DEL_PARTY(0)
_EVT_106_FMAN_END;
@!

;---------------------------------------


;#107	スペーストンネル1.5失敗(黒焦げ)

;★マジカントで黄金のオスカーを倒してサターンバレーにテレポートしてくると、
;広場には完成したスペーストンネルが置いてある。
;★プレイヤーキャラがスペーストンネルに接触したら一人ずつ乗り込んで、スタート!
;しかしガタガタ揺れた後、「ボカン!」で、中から(テレポート失敗の)黒こげの
;プレイヤーパーティーを出現させる。
;★ここでアンドーナツ博士を近寄らせてセリフ MSG_EVENT_XYZ_ANDONUT を発行。
;メッセージ後暗転。ホテルのシークエンスを挿入して、プレイヤーパーティーを
;サターンバレーの病院のベッドに出現させる。

MSG_EVT_SPACETUNNEL_KUROKOGE;
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_INTO_ST_KUROKOGE,OBJFX_SHOW_NONE);乗り込む
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
...;@WI(1)
...;@SET_CAMERA_GOM_CHAR(GOM_HIT_CHR)
...;@FRESH_PLAYER(PC_ALL)
...;@WAITSYS()
...;@SET_CAMERA_PARTY()
...;@COLD_PLAYER(PC_ALL)
@HIDE_PLAYER(PC_ALL,OBJFX_HIDE_NONE)
@BGMSTART(0,BGM_sptn)
@FRESH_CHAR(PRSN_DOSE_FIELD_3A);ST振動
@WAITSYS()
@BGMSTART(0,BGM_msc8);ボカン!

@WI( 100 )
@SET_SPECIAL(PC_NESS,8,2);黒こげへ

@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_OUT_ST_KUROKOGE,OBJFX_SHOW_NONE);出てくる
@SHOW_PLAYER(PC_ALL,OBJFX_SHOW_NONE)
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
...;@WI(1)
...;@SET_CAMERA_GOM_CHAR(GOM_HIT_CHR)
...;@FRESH_PLAYER(PC_ALL)
...;@WAITSYS()
...;@SET_CAMERA_PARTY()
...;@COLD_PLAYER(PC_ALL)
@WI( 120 )	;失敗音終了待ち
@REMOVE_CHAR(PRSN_DOSE_FIELD_K,ANIM_AND_DOSE);博士近寄る
@WAITSYS()
@MUSISTART()
@COLD_CHAR( PRSN_DOSE_FIELD_K )	;足踏み停止
@SET_SPECIAL(PC_NESS,8,0);正常化

MSG_EVENT_XYZ_ANDONUT;
;スペーストンネル失敗で黒こげのネスたちに
;アンドーナツ博士のオートメッセージ
;メッセージ後暗転して病院で目覚める
@OPEN(1)
◆Hmmm...it doesn’t work.↓
  One thing is missing...@KEY()↓
◆But that one material cannot normally be found on the earth.@KEY()↓
◆The material I’m thinking of came from a meteorite@KEY()↓
◆that fell when I was much younger.
;jpn:◆うーむ。やっぱりだめか。↓
;jpn: ざいりょうが ひとつ↓
;jpn: たりなかった。@KEY()↓
;jpn:◆しかしそれは↓
;jpn: この ちきゅうじょうの↓
;jpn: ぶっしつではないんだ。@KEY()↓
;jpn:◆あれは わしが まだわかいころ‥@KEY()↓
;jpn:◆おちてきた いんせきから↓
;jpn: さいしゅしたものなんだ。
@KEY()↓
_EVENT_ANDONUT_MAIN;
◆Have you seen a meteorite anywhere recently?↓
<Yes> <No>
;jpn:◆さいきん どこかで いんせきを↓
;jpn: みなかったかね?@KEY()↓
;jpn:  <みた>  <みなかった>
@DSP_ITEM(2)
@SELGOTO(_EVENT_XYZ_ANDONUT_YES,_EVENT_XYZ_ANDONUT_NO)

_EVENT_XYZ_ANDONUT_NO;
◆Hmmm...so are we just going to wait for a meteorite@KEY()↓
◆to suddenly fall from the sky?@KEY()↓
◆Mmm...you haven’t seen a meteorite...Hm...think for a minute...
;jpn:◆うーむ。それでは いつか↓
;jpn: いんせきがおちるまで きながに↓
;jpn: まつとしようか。@KEY()↓
;jpn:◆‥‥そうか‥‥@WI(60)↓
;jpn: みなかったのか‥‥。@KEY()↓
;jpn:◆うーむ。@WI(20)↓
;jpn: ‥‥まてよ‥‥
@KEY()↓
@GOTO(_EVENT_ANDONUT_MAIN)

;@CLOSEALL()
;@SETF(FLG_INFO_XYZ)
;@SETF(FLG_ONET_DOOR_CLOSE)
;@WARP(WARP_DOSEI_HOSPITAL_BED)
;』

_EVENT_XYZ_ANDONUT_YES;
◆Eureka!  That’s it!@KEY()↓
◆With even just a piece of the meteorite, I can synthesize the material Zexonyte.@KEY()↓
◆I doubt that your mortal enemy Giygas@KEY()↓
◆or whatever will allow you to go to Onett and get a piece of the meteorite.@KEY()↓
◆By now, Onett is in his evil hands.@KEY()↓
◆You must go, though, so take every possible precaution.@KEY()↓
◆The Saturnians have introduced some of their new developments at their shop.@KEY()↓
◆Please check them out before you leave.@KEY()↓
◆...I wonder what Onett is like at this point in time?...
;jpn:◆それだ!@KEY()↓
;jpn:◆そのいんせきの↓
;jpn: かけらでもあれば@KEY()↓
;jpn:◆ぶっしつXYZを↓
;jpn: ごうせいできるぞ。@KEY()↓
;jpn:◆‥‥しかし きみたちが↓
;jpn: オネットに いんせきのかけらを↓
;jpn: とりにいくことを@KEY()↓
;jpn:◆ギーグとかいう てきが↓
;jpn: かんたんに ゆるすとは↓
;jpn: おもえない。@KEY()↓
;jpn:◆おそらく オネットは↓
;jpn: すでに やつらのてに↓
;jpn: おちているにちがいない。@KEY()↓
;jpn:◆ちゅういの うえにも↓
;jpn: ちゅういを かさねて↓
;jpn: オネットに むかってくれ。@KEY()↓
;jpn:◆どせいさんの かいはつした↓
;jpn: きょうりょくな そうびが↓
;jpn: 「しんはつばい」された。@KEY()↓
;jpn:◆ぜひ それを↓
;jpn: みにつけていってくれ。@KEY()↓
;jpn:◆‥‥オネットのまちは↓
;jpn: どんなようすなのか‥‥。
@KEYNP()
@CLOSEALL()
@SETF(FLG_INFO_XYZ)
@SETF(FLG_ONET_DOOR_CLOSE)
@SETF(FLG_ONET_POKEY_DOOR_CLOSE)
@CLRF(FLG_OPEN_TABIGOYA);旅芸人の小屋にも入れない
@CLRF(FLG_ONET_MIZUNO_DOOR_OPEN)
@CLRF(FLG_ONET_DAYBREAK);オネット夜に
@CLRF(FLG_MYHOME_LIGHT_ON);マイホーム暗く
;@WARP(WARP_DOSEI_HOSPITAL_BED)
@CHKF(FLG_GOT_CAPEESTATE)@FALSE_GOTO(MSG_END)	;不動産買ってても
@CLRF(FLG_ONET_ESTATE_DOOR_OPEN)		;入れない。
』

;------------------------------------------------

;#108	●スペーストンネル２完成	16.1	-

;●FLG_XYZ_OK が立っていて FLG_DAY_AFTER が下りていれば
;スペーストンネルが完成している。
;●このタイミングで音楽をドラマチックなものに切り替えたい。

;---------------------------------------------------------


;#109	スペーストンネル２(ワープ)	16.1

;★スペーストンネルの瞬間空間移動の演出。

MSG_EVT_ST2;
;-----
@SETF(FLG_SYS_DIS_2H_PAPA)		;以降二時間パパ出ない。
@GOSUB(MSG_GLOBAL_DELIVERY_RESET)	;場面転換時に配達を中止
;-----
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_INTO_ST2,OBJFX_SHOW_NONE)  ;乗り込む
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
...;@WI(1)
...;@SET_CAMERA_GOM_CHAR(GOM_HIT_CHR)
...;@FRESH_PLAYER(PC_ALL)
...;@WAITSYS()			;乗り込みを待つ
...;@SET_CAMERA_PARTY()
...;@COLD_PLAYER(PC_ALL)
@HIDE_PLAYER(PC_ALL,OBJFX_HIDE_NONE)
;-------
;maltaムシ参照に変更	@BGMSTART(0,BGM_sptn)
@SETF( FLG_BGM_PUBL_WARP )
@MUSISTART()
;-------
@HIDE_CHAR( PRSN_DOSE_FIELD_3A, OBJFX_HIDE_VSTRIPE )
@WI( 180 )	;OBJFX_HIDE_VSTRIPEの処理終了待ちウェイト
@WARP(WARP_ST2_DKFD)            ;現在の最低国へワープ
@WI( 100 )
@MOVE_CHAR( PRSN_CHKA_SAITEI_3B, ANIM_CHKA_ST2_STILL, OBJFX_SHOW_VSTRIPE )
@WI( 80 )
;---------
@GOSUB( MSG_SUB_BGM_FADEOUT_LONG )	;BGMフェードアウト長め
@RESF( FLG_BGM_PUBL_WARP )
@MUSISTART()	;現在の最低国フィールドBGM
;---------
@SHOW_PLAYER(PC_ALL,OBJFX_SHOW_NONE)
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_OUT_ST2,OBJFX_SHOW_NONE)
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
...;@WI(1)
...;@SET_CAMERA_GOM_CHAR(GOM_HIT_CHR)
...;@FRESH_PLAYER(PC_ALL)
...;@WAITSYS()
...;@SET_CAMERA_PARTY()
...;@COLD_PLAYER(PC_ALL)
@SET_COMEBACK_POS(TLPT_ONETT);グッズテレポートは禁止
@SET_HIT_RECT(1,HRECT_DKFD_ST3_A,MSG_DKFD_HRECT_ST3_A)
@SET_HIT_RECT(2,HRECT_MABOROSHI_2,MSG_DKFD_OLDMAN)
@SETF(FLG_DKFD_ST2_APPEAR)	;以後最低国にスペーストンネル表示
』

;---------------------------------------------------

MSG_DKFD_HRECT_ST3_A;
@RES_HIT_RECT(1)
@SET_HIT_RECT(1,HRECT_DKFD_ST3_B,MSG_DKFD_HRECT_ST3_B)
』

;#110	ネスたち機械化	-	-
;#111	スペーストンネル３(タイムワープ)

;★現在の最低国。つの状のマップオブジェクトへの接近をきっかけに
;スペーストンネル３を出現させる。空間移動で登場の演出。
;★スペーストンネル３からは、アンドーナッツ博士、
;アップルキッド、土星さんが降りてくる。それぞれ MSG_DKFD_END_ANDONUT_1 、
;MSG_DKFD_END_APPLEKID 、MSG_DKFD_END_DOSEI 。
;●上記の３つのメッセージを聞くと、FLG_DKFD_END_GET_READY が ON になる。

;★博士のセリフ分岐先の_DKFD_END_ANDONUT_2_YES でフェイドアウト。

;★時間移動の演出。戦闘背景に使用しているサイクル画面を重ねる。

MSG_DKFD_HRECT_ST3_B;２つ目のHRECTに入ったところで以下を発行
@BGMSTART(0,BGM_msc9)
;@WI(120)
;@WI(60)
@MOVE_CHAR(PRSN_CHKA_SAITEI_3C,ANIM_ST_ST3,OBJFX_SHOW_VSTRIPE)       ;ST登場
@WI(120)
@WI(60)
@MOVE_CHAR(PRSN_CHKA_SAITEI_F,ANIM_DOSEI_ST3,OBJFX_SHOW_NONE)        ;土星さん
@MOVE_CHAR(PRSN_CHKA_SAITEI_C,ANIM_AND_ST3_1,OBJFX_SHOW_NONE);アンドーナツ博士
@MOVE_CHAR(PRSN_CHKA_SAITEI_E,ANIM_KID_ST3,OBJFX_SHOW_NONE)  ;アップルキッド
@WAITSYS()
@MUSISTART()
@GET_CHAR_DIR( PRSN_CHKA_SAITEI_C, 1, PC_ALL )
@SET_CHAR_DIR( PRSN_CHKA_SAITEI_C, 0 )
@COLD_CHAR( PRSN_CHKA_SAITEI_F )	;土星さん
@COLD_CHAR( PRSN_CHKA_SAITEI_C )	;アンドーナツ博士
@COLD_CHAR( PRSN_CHKA_SAITEI_E )	;アップルキッド
@GOSUB(MSG_DKFD_END_ANDONUT_1)
@SETF(FLG_DKFD_ANDONUT_A_APPEAR)
@SETF(FLG_DKFD_APPLE_A_APPEAR)
@SETF(FLG_DKFD_DOSEI_A_APPEAR)
@SETF(FLG_DKFD_ST3_APPEAR)
』

MSG_DKFD_END_ANDONUT_1;
;現在の最低国にスペーストンネルで登場の博士たち
@CHKFGOTO(FLG_DKFD_END_GET_READY,MSG_DKFD_END_ANDONUT_2)
@OPEN(1)
◆The Phase Distorter has been completed.@KEY()↓
◆We could finish it quickly because of Mr. Saturn’s incredible scientific skill...@KEY()↓
◆Giygas is attacking from our exact location,@KEY()↓
◆but he is attacking from many years in the past.
;jpn:◆あっというまに↓
;jpn: 「スペーストンネル３」が↓
;jpn: かんせいしたぞ。@KEY()↓
;jpn:◆どせいさんたちの↓
;jpn: かがくぎじゅつは すごいぞ!@KEY()↓
;jpn:◆ギーグは このおなじばしょから↓
;jpn: つまり かこの このばしょから↓
;jpn: こうげきを しかけているんだ。
@KEY()
@CLOSEALL()
』

MSG_DKFD_ANDONUT_A;
◆This is very hard for me to tell you, but...
;jpn:◆いいにくいのだが‥‥。
@KEY()
@CHKFGOTO(FLG_DKFD_END_GET_READY,_DKFD_ANDONUT_A_READY)
』

_DKFD_ANDONUT_A_READY;
;アップルキッド、土星さんと話した後でアンドーナツ博士に話すと、以下セリフ…
@CLOSEALL()
@HIDE_CHAR(PRSN_CHKA_SAITEI_C,OBJFX_HIDE_NONE)
@MOVE_CHAR(PRSN_CHKA_SAITEI_D,ANIM_AND_ST3_2,OBJFX_SHOW_NONE);アンドーナツ博士移動
@WI( 150 )
@SETF(FLG_DKFD_ANDONUT_B_APPEAR)
@CLRF(FLG_DKFD_ANDONUT_A_APPEAR)
』

MSG_DKFD_END_ANDONUT_2;
◆In order to defeat Giygas,@KEY()↓
◆who is attacking from the past, you must warp to the past.@KEY()↓
◆This can only be done by way of the ”Phase Distorter ３.”@KEY()↓
◆However, the machine cannot warp living things, I mean lifeforms.@KEY()↓
◆Life is demolished in the process of warping.@KEY()↓
◆The only way to accomplish the time travel is@KEY()↓
◆to transfer your brain ”program” into a robot,@KEY()↓
◆and send the robot to the past.@KEY()↓
◆...The transfer means that your spirit will go with the robot@KEY()↓
◆while your body is left behind...@KEY()↓
◆I cannot promise that your spirit will come back after the battle in the past.@KEY()↓
◆Yet, you must understand that the four of you are the chosen ones.@KEY()↓
◆Do you still wish to face Giygas by traveling to the past?@KEY()↓
<Yes> <No>
;jpn:◆@DSP_STS(STS_PC_NAME_NES)!↓
;jpn: かこから こうげきをしてくる↓
;jpn: ギーグをたおすには@KEY()↓
;jpn:◆かこにワープすることが↓
;jpn: ひつようだ。 むろん‥‥@KEY()↓
;jpn:◆それは「スペーストンネル３」で↓
;jpn: できることなんだが‥‥@KEY()↓
;jpn:◆‥‥このそうちは いきものを‥‥↓
;jpn: 「せいめいたい」を↓
;jpn: ワープさせることが できない。@KEY()↓
;jpn:◆ワープのプロセスで いのちは↓
;jpn: きえさってしまうんだ。@KEY()↓
;jpn:◆ほうほうは ただ ひとつ。@KEY()↓
;jpn:◆きみたちの ずのうプログラムを↓
;jpn: ロボットに いしょくして↓
;jpn: かこへ ゆくしかない。@KEY()↓
;jpn:◆‥‥そのばあい↓
;jpn: きみたちのスピリットは↓
;jpn: ロボットのほうに いってしまう。@KEY()↓
;jpn:◆きみたちのにくたいは↓
;jpn: ただのぬけがらに なるわけだ。@KEY()↓
;jpn:◆‥‥かこでの たたかいがおわって↓
;jpn: スピリットがもどってくるという↓
;jpn: かのうせいは よそくできない。@KEY()↓
;jpn:◆えらばれた４にんのしょくん!↓
;jpn: それでも かこのせかいに↓
;jpn: いってくれるか?@KEY()↓
;jpn:  <はい>  <いいえ>
@DSP_ITEM(2)
@SELGOTO(_DKFD_END_ANDONUT_2_YES,_DKFD_END_ANDONUT_2_NO)

_DKFD_END_ANDONUT_2_NO;
◆Yes, I gave you the chance to refuse...it’s natural to feel as you do.@KEY()↓
◆The only thing left is the destruction of our planet...
;jpn:◆それも ひとつの こたえだ。@WI(20)↓
;jpn: このまま すべてが ほろびるのを↓
;jpn: ‥‥まっていようか。
@KEYNP()
』

_DKFD_END_ANDONUT_2_YES;
◆...Hmmm...you know that you may not be able to return to your current form,@KEY()↓
◆right?↓
<Yes> <No>
;jpn:◆‥‥‥@WI(20)わかった。@KEY()↓
;jpn:◆もう いままでの↓
;jpn: きみたちの すがたに↓
;jpn: もどれないかもしれないんだが@KEY()↓
;jpn:◆‥‥ほんとうに それでも@WI(20)↓
;jpn: ‥‥‥いいか?@WI(30)↓
;jpn:  <はい>  <いいえ>
@DSP_ITEM(2)
@SELGOTO(_DKFD_END_ANDONUT_YES_YES,_DKFD_END_ANDONUT_2_NO)
@GOTO(_DKFD_END_ANDONUT_2_NO)

_DKFD_END_ANDONUT_YES_YES;
◆...Yes...I see...@KEY()↓
◆You have really set your mind on this...@KEY()↓
◆Let me take a good look at you now...@KEY()↓
◆@DSP_NAME(PC_NESS), please give me that red cap...
;jpn:◆‥‥‥@WI(20)そうか。@WI(30)↓
;jpn: そこまでの‥‥@KEY()↓
;jpn:◆きみたち せめて わたしに↓
;jpn: いまの きみたちの すがたを↓
;jpn: よくみせてくれ。@KEY()↓
;jpn:◆‥‥@DSP_STS(STS_PC_NAME_NES)@WI(20)↓
;jpn: そのぼうしを わたしに‥‥
@KEY()↓
@WI(60)↓
◆Ready...stand by...
;jpn:◆よし スタンバイだ!
@KEY()
@CLOSEALL()
@WI(1)
;-----
@GOSUB(MSG_GLOBAL_DELIVERY_RESET)	;場面転換時に配達を中止
;-----
;改造人間手術(ショッカーではない)
@BGM_EFFECT( 3 )		;BGMフェードアウト長め
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_INTO_NIGHT, OBJFX_SHOW_NONE )
@WAITSYS()
@HIDE_PLAYER( PC_ALL, OBJFX_HIDE_NONE )
@WARP( WARP_PUBLIC_BLACKSCREEN )
@COLD_PLAYER( PC_ALL )
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_REMODEL_OPERATION, OBJFX_SHOW_NONE )
@WAITSYS()
@COLD_CHAR( GOM_HIT_CHR )
@BGMSTART( 0, BGM_mm01 )	;「ちゅいーん ガガガ…」
@FRESH_CHAR( GOM_HIT_CHR )
@WAITSYS()
@BGMSTOP( 0 )
;----------------
@MOVE_GOM_CHAR( GOM_ANDOUGHNUT, ANIM_AND_NEAR_ST3, OBJFX_AFTER_WARP )
@WARP(WARP_DKFD_ST3_START)
@COLD_PLAYER(PC_ALL)
@HIDE_PLAYER(PC_ALL,OBJFX_HIDE_NONE)
MSG_DKFD_END_ANDONUT_3;
@OPEN(1)
◆@DSP_NAME(PC_NESS)!@KEY()↓
◆@DSP_NAME(PC_PAULA)!@KEY()↓
◆@DSP_NAME(PC_POO)!@KEY()↓
◆@DSP_NAME(PC_JEFF)!...my son...@KEY()↓
◆There is no turning back now.@KEY()↓
◆@DSP_NAME(PC_NESS), activate the Phase Distorter ３ by your own hand,@KEY()↓
◆thereby following your own destiny...will you do so?↓
<Yes> <No>
;jpn:◆@DSP_STS(STS_PC_NAME_NES)!↓
;jpn: @DSP_STS(STS_PC_NAME_PAULA)!↓
;jpn: @DSP_STS(STS_PC_NAME_POO)!@KEY()↓
;jpn:◆そして‥‥わたしのむすこ↓
;jpn: ‥‥@DSP_STS(STS_PC_NAME_JEFF)!@KEY()↓
;jpn:◆‥‥コクピットに↓
;jpn: ‥‥すわってくれ。@KEY()↓
;jpn:◆もう あともどりは できないぞ。@WI(30)↓
;jpn: ‥‥‥せめて @DSP_STS(STS_PC_NAME_NES)くん@WI(15)↓
;jpn: 「スペーストンネル」のスイッチを@KEY()↓
;jpn:◆じぶんのてで おしてくれ。@KEY()↓
;jpn:  <はい>  <いいえ>
@DSP_ITEM(2)
@SELGOTO(_DKFD_END_ANDONUT_3_YES,_DKFD_END_ANDONUT_3_NO)
@GOTO(_DKFD_END_ANDONUT_3_NO)

_DKFD_END_ANDONUT_3_NO;
◆...I see...@KEY()↓
◆@DSP_STS(STS_PC_NAME_JEFF)!  You do it!↓
<Yes> <No>
;jpn:◆‥‥そうか@WI(20) そうだろう。@KEY()↓
;jpn:◆@DSP_STS(STS_PC_NAME_JEFF)!@WI(20)↓
;jpn: ‥‥おまえが おしなさい。@WI(30)↓
;jpn:  <はい>  <いいえ>
@DSP_ITEM(2)
@SELGOTO(_DKFD_END_ANDONUT_3_YES,_DKFD_END_ANDONUT_3_NO_NO)
@GOTO(_DKFD_END_ANDONUT_3_NO_NO)

_DKFD_END_ANDONUT_3_NO_NO;
◆Oh, you lost your nerve...@KEY()↓
◆@DSP_NAME(PC_NESS)!  It is up to you...↓
<Yes> <No>
;jpn:◆‥‥よ@WI(15) よわむしめ!@WI(30)↓
;jpn: やっぱり @DSP_STS(STS_PC_NAME_NES)くん@WI(15)↓
;jpn: きみが‥‥@KEY()↓
;jpn:  <はい>  <いいえ>
@DSP_ITEM(2)
@SELGOTO(_DKFD_END_ANDONUT_3_YES,_DKFD_END_ANDONUT_3_NO)

_DKFD_END_ANDONUT_3_YES;
◆Only a few people know of your amazing courage,@KEY()↓
◆but the number of people you save through that bravery is immense.@KEY()↓
◆I feel very fortunate@KEY()↓
◆to be present at the beginning of this monumental undertaking...
;jpn:◆きみたちの しんの ゆうきを↓
;jpn: しっている にんげんの かずは↓
;jpn: ほんの ひとにぎりだが‥‥@KEY()↓
;jpn:◆きみたちが その ゆうきで↓
;jpn: すくう ひとびとの かずは↓
;jpn: はかりしれない。@KEY()↓
;jpn:◆‥‥こんな ばめんに↓
;jpn: いあわせることのできた わたしは@WI(30)↓
;jpn: ‥‥‥@WI(20)しあわせものだ。
@KEY()
@BGMSTART(0,BGM_sptn);虫を無視
@CLOSEALL()
@SETF(FLG_STEP_PAST)	;祈る人々ST２出現用
@WI(1)
@WARP(WARP_ST3_PAST)
@COLD_PLAYER( PC_ALL )
@WI( 60 )
@SHOW_PLAYER(PC_ALL,OBJFX_SHOW_NONE)
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_OUT_ST3,OBJFX_SHOW_NONE)
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
...;@WI(1)
...;@SET_CAMERA_GOM_CHAR(GOM_HIT_CHR)
...;@FRESH_PLAYER(PC_ALL)
...;@WAITSYS()
...;@SET_CAMERA_PARTY()
...;@COLD_PLAYER(PC_ALL)
@SET_COMEBACK_POS(TLPT_ONETT);グッズテレポートは禁止
@CLRF(FLG_DKFD_DOSEI_ST1_APPEAR)
@CLRF(FLG_DKFD_ANDONUT_A_APPEAR)
@CLRF(FLG_DKFD_ANDONUT_B_APPEAR)
@CLRF(FLG_DKFD_APPLE_A_APPEAR)
@CLRF(FLG_DKFD_DOSEI_A_APPEAR)
@CLRF(FLG_DKFD_ST2_APPEAR)
@CLRF(FLG_DKFD_ST3_APPEAR)
@CLRF(FLG_SUMS_STOIC_RESERVED);ストイッククラブに入れない
@CLRF(FLG_SYS_MONSTER_OFF)
;-----
@FRESH_PLAYER(PC_ALL)
』

MSG_DKFD_END_APPLEKID;
◆You must warp to the past and fight, but...
;jpn:◆「スペーストンネル３」で↓
;jpn: かこへ ワープして↓
;jpn: たたかわなくては‥‥@KEY()↓
;jpn:◆でも‥‥
@KEYNP()
@SETF(FLG_DKFD_END_GET_READY)
』

MSG_DKFD_END_DOSEI;
@FONTBAKA()
◆We here too.@KEY()↓
◆Boing!
;jpn:◆わしらも きたです。@KEY()↓
;jpn:◆ポエーン
@KEYNP()
@FONTSTD()
』

;-------------------------------------------------------------
;#112	最終戦闘

MSG_EVT_112_LASTBATTLE;
;------------
@BGM_EFFECT(3)
@WI(180)
@BGMSTART(0,BGM_gegu2)
;------------
@MOVE_GOM_CHAR( GOM_HIT_CHR2, ANIM_STAGE_PARTY, OBJFX_SHOW_NONE ) ;固まるプレーヤー
@WI( 1 )	;GOM_HIT_CHR2の登録待ち
@SET_CAMERA_GOM_CHAR( GOM_HIT_CHR2 )
@FRESH_PLAYER( PC_ALL )
;画面が揺れる ＆ 虚像マシン→ネスの顔に変化
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_LAST_BATTLE, OBJFX_SHOW_NONE )
@WAITSYS()	;ANIM_LAST_BATTLEより
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_LAST_BATTLE, OBJFX_SHOW_NONE )
@MOVE_GOM_CHAR( GOM_NES_FACE, ANIM_NES_FACE, OBJFX_SHOW_NONE )
@WAITSYS()	;ANIM_LAST_BATTLEより
@MOVE_GOM_CHAR( GOM_HIT_CHR, ANIM_LAST_BATTLE, OBJFX_SHOW_NONE )
@WAITSYS()	;ANIM_LAST_BATTLEより
@SET_CAMERA_PARTY()
@COLD_PLAYER(PC_ALL)
@HIDE_GOM_CHAR( GOM_HIT_CHR2, OBJFX_HIDE_NONE )
@WI( 120 )
;ポーキーが降りてくる
@SE( SE_se46_b_rocket )
;-----ネスの顔を向かせる
@GET_ORDER_PLAYER(1)
@GET_PLAYER_DIR(0,3,GOM_NES_FACE)
@SET_PARTY_DIR(0)
;-----
@MOVE_GOM_CHAR( GOM_MECHPOKEY, ANIM_LAST_BATTLE_POKEY, OBJFX_SHOW_DOTS )
@WAITSYS()
@WI( 100 );間
;ここでポーキーの台詞
@OPEN( 1 )
@GOSUB( MSG_PAST_POKEY )
@CLOSEALL()
@WI(120)
;最終戦闘へ
@GOSUB( MSG_BATTLE_FINAL )	;global.msg
@TRUE_GOTO(MSG_BATTLE_LOSE)
@SETF(FLG_WIN_GIEGU)
@CLRF(FLG_ONET_DOOR_CLOSE)
@SETF(FLG_ONET_MIZUNO_DOOR_OPEN)
@SETF(FLG_OPEN_TABIGOYA);旅芸人の小屋にも入れる
@CLRF(FLG_ONET_POKEY_DOOR_CLOSE)
@CLRF(FLG_FOUR_MONOTOLY_APPEAR)	;モノトリー室からモノトリー消える
@SETF(FLG_ONET_LARDNA_APPEAR)	;ポーキーママ出現
@SETF(FLG_ONET_DAYBREAK)	;オネット夜明け
@SETF(FLG_MYHOME_LIGHT_ON)	;マイホーム明るく
@CLRF(FLG_STEP_PAST);祈る人々消す
@SETF(FLG_DOG)			;イヌを仲間にしていなくても
@SETF(FLG_DOG_LATER_APPEAR)	;うろつきイヌに変更
;-----
@SETF(FLG_SYS_MONSTER_OFF);最終戦闘後は敵を出さない
;@DIS_HPDEC();HPは減少しない
@FUNC(5)			;毒死配達禁止
;-----
;@GOSUB(MSG_DEBUG_EVENT_112)
;ありません ;最終戦闘から抜けた時に@SET_CAMERA_PARTY()の必要があります
; @SET_CAMERA_PARTY()
;----------------------------------------
;#113	ネスたちの魂の帰還
;@DIS_BGMCHG();虫を無視
@BGMSTOP(0)
;-----
@GOSUB(MSG_GLOBAL_DELIVERY_RESET)	;場面転換時に配達を中止(念の為)
;-----
@GOSUB(MSG_SUB_ALLHEAL_POWR)	;全回復しておく
;-----------
@HIDE_PLAYER(PC_ALL,OBJFX_HIDE_NONE)
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_WHITEOUT,OBJFX_SHOW_NONE)	;白フェードアウト
@WAITSYS()
@WARP(WARP_PAST_RETURN)
@COLD_PLAYER(PC_ALL)
;---					;脱退前に記念撮影(隠し撮り)
@SETF(FLG_PHOTO_SATURN);撮り頃フラグ	;隠し撮り用に変更;おじさん隠し撮り
@GOSUB(MSG_GLOBAL_PHOTO_32)		;撮影
;---
@DEL_PARTY(PC_POO)		;プー脱退
@DEL_PARTY(PC_JEFF)		;ジェフ脱退
;-----
@WI(180)
@MESSAGE_SOUND(3)
@OPEN(14)
;jpn: @DSP_STS(STS_PC_NAME_NES)たちの たたかいは@WI(15) おわった。@KEYNP()@WI(30)
@CLOSEALL()
@MESSAGE_SOUND(1)
@WI(30)
;ボタン待ち
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_NES,OBJFX_SHOW_NONE)
@WAITSYS()
@COLD_GOM_CHAR(GOM_LIGHTBALL)
@SETF(FLG_BGM_SOUL)
@MUSISTART()
;-----
;@BGMSTART(0,BGM_mapu)
@WI(120)
@WI(120)
@WI(120)
@WI(120)
@WI(120)
;---------------
@BGM_EFFECT(27)
@FRESH_GOM_CHAR(GOM_LIGHTBALL)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_POLA,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_JEF,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_POO,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WAITSYS()					;白フェードアウト
;サターンバレー
@SETF(FLG_DOSEI_APPLE_APPEAR)			;ほんとは既にセットされているはず
@MOVE_GOM_CHAR( GOM_NES_DOWN, ANIM_DOSEI_NES_DOWN, OBJFX_AFTER_WARP )
@MOVE_GOM_CHAR( GOM_POLA_DOWN,ANIM_DOSEI_POLA_DOWN,OBJFX_AFTER_WARP )
@MOVE_GOM_CHAR( GOM_JEF_DOWN, ANIM_DOSEI_JEF_DOWN, OBJFX_AFTER_WARP )
@MOVE_GOM_CHAR( GOM_POO_DOWN, ANIM_DOSEI_POO_DOWN, OBJFX_AFTER_WARP )
@WARP(WARP_DOSE_RETURN)
@BGMSTART(0,BGM_mm03)
;@HIDE_CHAR(PRSN_DOSE_FIELD_3A,OBJFX_HIDE_NONE)	;とりあえず
@COLD_PLAYER(PC_ALL)
;@COLD_CHAR(PRSN_DOSE_FIELD_B)		;どせいさんを止めておく
;始め倒れている
@WI(100)
@WI(100)
@WI(100)
@WI(100)
@WI(100)
;-------------
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_POO2,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
;@HIDE_CHAR(PRSN_DOSE_FIELD_U,OBJFX_HIDE_NONE)
@HIDE_GOM_CHAR( GOM_POO_DOWN, OBJFX_HIDE_NONE )
@MOVE_GOM_CHAR( GOM_POO,ANIM_COME_BACK_POO,OBJFX_SHOW_NONE)
@WAITSYS()
@COLD_GOM_CHAR(GOM_POO)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_JEF2,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
;@HIDE_CHAR(PRSN_DOSE_FIELD_T,OBJFX_HIDE_NONE)
@HIDE_GOM_CHAR( GOM_JEF_DOWN,OBJFX_HIDE_NONE)
@REMOVE_CHAR(PRSN_DOSE_FIELD_V,ANIM_COME_BACK_JEF)	;ジェフ
;@MOVE_GOM_CHAR(GOM_JEF,ANIM_COME_BACK_JEF,OBJFX_SHOW_NONE)
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_POLA2,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
@WI(120)
;@HIDE_CHAR(PRSN_DOSE_FIELD_S,OBJFX_HIDE_NONE)
@HIDE_GOM_CHAR( GOM_POLA_DOWN, OBJFX_HIDE_NONE)
@MOVE_GOM_CHAR(GOM_POLA,ANIM_COME_BACK_POLA,OBJFX_SHOW_NONE)
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
@BGM_EFFECT(27)
@MOVE_GOM_CHAR(GOM_LIGHTBALL,ANIM_SPIRIT_RETURN_NES2,OBJFX_SHOW_NONE)
@WAITSYS()
@BGM_EFFECT(28)
;---------------
@WI(120)
;@HIDE_CHAR(PRSN_DOSE_FIELD_R,OBJFX_HIDE_NONE)
@HIDE_GOM_CHAR( GOM_NES_DOWN, OBJFX_HIDE_NONE )
@MOVE_GOM_CHAR( GOM_NES, ANIM_COME_BACK_NES, OBJFX_SHOW_NONE)
@WAITSYS()
@COLD_GOM_CHAR(GOM_NES)
@WI(60)
;@ENA_BGMCHG()
;-------------
@CLRF(FLG_BGM_SOUL)
@GOSUB(MSG_SUB_BGM_FADEOUT_2SECOND)
;@WI(120)
@MUSISTART()
;-------------
;向き合う
@FRESH_GOM_CHAR(GOM_POO)
@WAITSYS()
@COLD_GOM_CHAR(GOM_POO)
@FRESH_GOM_CHAR(GOM_POLA)
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
@FRESH_GOM_CHAR(GOM_NES)
@WAITSYS()
@COLD_GOM_CHAR(GOM_NES)
@WI(40)
;プーの別れ
;@SHOW_MANPU_GOM_CHAR(GOM_POO,MANPU_SURPRISE)
@WI(30)
@GOSUB(MSG_DOSEI_POO)
;@HIDE_MANPU_GOM_CHAR(GOM_POO)
@WI(60)
@OPEN(1)
◆I’ll see you again someday!
;jpn:◆また いつか あおう!
@KEYNP()
@CLOSEALL()
@WI(30)
;プー消える
@FRESH_GOM_CHAR(GOM_POO)		;自動で自殺
@WAITSYS()
@SE(SE_se63_teleport_appear)
@WAITSYS()
@MOVE_GOM_CHAR(GOM_EXPLOSION,ANIM_POO_EXPLOSION,OBJFX_SHOW_NONE)
@WI(120)
@WI(60)
;プレゼントが落ちてくる
@MOVE_CHAR(PRSN_DOSE_FIELD_2B,ANIM_PRESENTBOX_FALL1,OBJFX_SHOW_NONE)
@WI(10)
@MOVE_GOM_CHAR(GOM_SHADOW_FALL,ANIM_PRESENTBOX_SHADOW1,OBJFX_SHOW_NONE)
@WAITSYS()
@MOVE_CHAR(PRSN_DOSE_FIELD_2C,ANIM_PRESENTBOX_FALL2,OBJFX_SHOW_NONE)
@WI(10)
@MOVE_GOM_CHAR(GOM_SHADOW_FALL,ANIM_PRESENTBOX_SHADOW2,OBJFX_SHOW_NONE)
@WAITSYS()
@MOVE_CHAR(PRSN_DOSE_FIELD_2D,ANIM_PRESENTBOX_FALL3,OBJFX_SHOW_NONE)
@WI(10)
@MOVE_GOM_CHAR(GOM_SHADOW_FALL,ANIM_PRESENTBOX_SHADOW3,OBJFX_SHOW_NONE)
@WAITSYS()
@SETF(FLG_DOSEI_PU_BOX_APPEAR)
@WI(60)
;ジェフの別れ
;@SHOW_MANPU_CHAR(PRSN_DOSE_FIELD_V,MANPU_SURPRISE)
;@WI(30)
;@GOSUB(MSG_DOSEI_JEFF)
;@HIDE_MANPU_CHAR(PRSN_DOSE_FIELD_V)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;ポーラに近づく
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
;@FRESH_GOM_CHAR(GOM_POLA)		;ジェフを見る
;@WAITSYS()
;@COLD_GOM_CHAR(GOM_POLA)
;@FRESH_GOM_CHAR(GOM_NES)		;ジェフを見る
;@WAITSYS()
;@COLD_GOM_CHAR(GOM_NES)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;握手する
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@WI(30)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;ネスに近づく
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@FRESH_GOM_CHAR(GOM_NES)		;ジェフを見る
@WAITSYS()
@COLD_GOM_CHAR(GOM_NES)
@FRESH_GOM_CHAR(GOM_POLA)		;ジェフを見る
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;握手する
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@WI(30)
@GOSUB(MSG_DOSEI_JEFF)
@WI(30)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;博士の横に
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@REMOVE_CHAR(PRSN_DOSE_FIELD_K,ANIM_DOSE_ANDONUT_END)		;博士
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_K)
@FRESH_GOM_CHAR(GOM_NES)		;ジェフを見る
@WAITSYS()
@COLD_GOM_CHAR(GOM_NES)
@FRESH_GOM_CHAR(GOM_POLA)		;ジェフを見る
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
@WI(60)
@REMOVE_CHAR(PRSN_DOSE_FIELD_B,ANIM_DOSE_DOSEI_END)		;どせいさん
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_B)
@WI(30)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;どせいさんを見る
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@WI(60)
@FRESH_CHAR(PRSN_DOSE_FIELD_V)		;上向く
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_V)
@WI(30)
@FRESH_CHAR(PRSN_DOSE_FIELD_B)
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_B)
@WI(15)
@FRESH_CHAR(PRSN_DOSE_FIELD_K)
@WAITSYS()
@COLD_CHAR(PRSN_DOSE_FIELD_K)
@WI(80)
@FRESH_GOM_CHAR(GOM_POLA)		;ネスの横に
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
@FRESH_GOM_CHAR(GOM_NES)		;ポーラを向く
@WAITSYS()
@COLD_GOM_CHAR(GOM_NES)
@WI(60)
@GOSUB(MSG_DOSEI_POLA)
@FRESH_GOM_CHAR(GOM_POLA)		;ネスと重なる
@WAITSYS()
@COLD_GOM_CHAR(GOM_POLA)
;@HIDE_CHAR(PRSN_DOSE_FIELD_V,OBJFX_HIDE_NONE)
;@MOVE_CHAR(PRSN_DOSE_FIELD_V,ANIM_DOSEI_JEFF_LATER,OBJFX_SHOW_NONE)
;ジェフに話しずらいのでどせいさんを動かす
@REMOVE_CHAR(PRSN_DOSE_FIELD_B,ANIM_PEOPLEWALK_NARROW)		;どせいさん
;ポーラの別れポイントセット
@RES_HIT_RECT(1)
@SET_HIT_RECT(1,HRECT_LAST_POLA_3,MSG_POLA_POLASTAR_BYE_PREPARE)
@RES_HIT_RECT(2)
@SET_HIT_RECT(2,HRECT_LAST_POLA_5,MSG_POLA_NESHOME_BYE)
@HIDE_GOM_CHAR(GOM_NES,OBJFX_HIDE_NONE)
@HIDE_GOM_CHAR(GOM_POLA,OBJFX_HIDE_NONE)
;土星谷にカムバックポジションをセットする
@SET_COMEBACK_POS(TLPT_DOSEIDANI)
;操作可能に
;@BGMSTOP(0)
;@ENA_BGMCHG()
;@MUSISTART()
@SHOW_PLAYER(PC_ALL,OBJFX_SHOW_NONE)
@FRESH_PLAYER(PC_ALL)
;-----
@FUNC(5)					;毒死配達禁止
@CLRF(FLG_PHOTO_RAMMA)		;撮り頃フラグ;プーが別れてからは撮れない
@CHKF(FLG_GOT_CAPEESTATE)@FALSE_GOTO(MSG_END)	;不動産を買ってたら
@SETF(FLG_ONET_ESTATE_DOOR_OPEN)		;ドア開く。
』

;----------------------------------------
;#114	●祝福
;----------------------------------------
;#115	エンディング・シークエンス
;----------------------------------------
;	●(青から元に戻っている)
;	●(「ちょっとかぎマシン」を使う)
;	●(魔テント出現)

;-------------------------------------------------

MSG_EVT_POLADAD_CLAMOR;松(ぽ、ぽがない?)
;娘を探して走るポーラの父
;ツーソンホテルからフィールドへのドアで、予約メッセージとして発行される
;メッセージを発行しながらキャラが動くという掟破りをしている
;
@WI( 20 )
;西画面外から走ってくるポーラ父
@MOVE_GOM_CHAR( GOM_POLA_FATHER, ANIM_EVT_POOLAAA, OBJFX_SHOW_NONE )
;ホテル前で周りを見るポーラ父
@WAITSYS()
@COLD_GOM_CHAR( GOM_POLA_FATHER )	;西画面外への移動ちょっと待て
;セリフ「◆ポォーーーラァーーー!!」を表示しながら
; ※３０文字以内に納めること!!
@SET_LOOPREG(1)
@GET_PLAYER_NAME(PC_PAULA)
@SET_REG(0)
@OPEN( 1 )
@WI(10)
◆@DSP_CHAR(0)@WI(10) @DSP_CHAR(0)@WI(10) @DSP_STS(STS_PC_NAME_PAULA)!!@WI(20)  Where are you!@WI(20)  It’s time for a yummy piece of pie!
;jpn:◆@DSP_CHAR(0)@WI(10) @DSP_CHAR(0)@WI(10) @DSP_STS(STS_PC_NAME_PAULA)ーーー!!
@WI(20)↓
;jpn: ど どこにいるんだー!↓
;jpn: お お おやつの じかんだぞー!
;西画面外へ走り去る父
@FRESH_GOM_CHAR( GOM_POLA_FATHER )	;うむ、行ってよし
@WAITSYS()
@CLOSEALL()
@SETF(FLG_TWSN_PAUL_CRY);泣くのは一度だけ
』
;-------------------------------------------------

MSG_EVT_CHIKA_GUMIDORIAN;
;地底大陸の扉の前に立つと、グミ族の一人がよってきて発行
; ANIM_SMELL_LIKE_GUMIDORIAN
@SHOW_MANPU_CHAR( PRSN_CHKA_FIELD_B, MANPU_BIKKURI )
@WI( 60 )
@HIDE_MANPU_CHAR( PRSN_CHKA_FIELD_B )
@FRESH_CHAR( PRSN_CHKA_FIELD_B )
@WAITSYS()		;扉まで来るのを待つ
@COLD_CHAR( PRSN_CHKA_FIELD_B )
@OPEN(1)
◆You guys smell really good!  ＊sniff, sniff, sniff＊@KEY()↓
◆Who am I?  I’m a  Tenda!
;jpn:◆おまえら けっこう すっげえ↓
;jpn: いいにおいするじゃん!@WI(15)クンクン@KEY()↓
;jpn:◆あっ オレ? おれもグミぞくよ。
@KEYNP()
@CLOSE()
@WI(10)
;ボスが近寄る
@REMOVE_CHAR( PRSN_CHKA_FIELD_C, ANIM_OPENGATE_GUMI )
@WAITSYS()
@COLD_CHAR( PRSN_CHKA_FIELD_C )
@OPEN(1)
◆Hey!  What are you doing inside our dinosaur cage?@KEY()↓
◆I’m going to open the door, and you get out of there right now!
;jpn:◆きょうりゅうの オリのなかで↓
;jpn: なにやってるんだ?!@KEY()↓
;jpn:◆いりぐちを あけてやるから↓
;jpn: はやく こっちに でてこい。@WI(20)↓
;jpn: ボスの オレが ゆるすから。
@KEYNP()
@CLOSE()
@FRESH_CHAR( PRSN_CHKA_FIELD_C )
@WAITSYS()
@COLD_CHAR( PRSN_CHKA_FIELD_C )
@HIDE_CHAR(PRSN_CHKA_FIELD_3B,OBJFX_HIDE_NONE)	;扉消す
@SETF(FLG_DKFD_DOOR_DISAPPEAR)			;扉もう出ない
@OPEN(1)
◆Thank you for the Tendakraut.
;jpn:◆‥‥グミドリアン ありがとよ。
@KEY()↓
@SE(SE_se76_my_give)
@GOODSOUT(PC_ALL,GDS_TOOL_KEY_GUMIDORIAN)
@SETF(FLG_DKFD_GUMI_BOSS);セリフ替え用(でもいまは使ってない)
@GET_ORDER_PLAYER(1)
@SET_REG(0)
◆(@DSP_NAME(0)’s Tendakraut was stolen.)@KEY()↓
◆(The awful smell surrounding the group is now gone.)
;jpn:◆(@DSP_NAME(0)は↓
;jpn: 「グミドリアン」を↓
;jpn: とられてしまった)@KEY()↓
;jpn:◆(もう くさく なくなった)
@KEY()
@CLOSEALL()
;@WI(1)
;@REMOVE_CHAR(PRSN_CHKA_FIELD_B,ANIM_MAPOBJSTILL)
;@REMOVE_CHAR(PRSN_CHKA_FIELD_C,ANIM_MAPOBJSTILL)
』

;-------------------------------------------------
MSG_EVT_TO_BE_CONTINUED;＠
@DEL_PARTY(PC_PAULA)	;ポーラ脱退
@FUNC(15)		;すべてのフラグクリア
@FUNC(5)		;配達毒死禁止
@SETF(FLG_MYHOME_TO_BE)	;ピッキー登場フラグ
@SETF(FLG_MYHOME_KNOCK_APPEAR)
@SETF(FLG_ONET_POKEY_DOOR_CLOSE)
@SETF(FLG_YAZIUMA_DISAPPEAR)
@SETF(FLG_YAZIUMA_POKEY)
@SETF(FLG_KAIDAN_TRACY_APPEAR)
@SETF(FLG_INSEKI_STOPPER_APPEAR)
@SETF(FLG_MYHOME_2F_1F)
@SETF(FLG_MYHOME_TRACY_DISAPPEAR)
@SETF(FLG_MYHOME_NES_CHANGE)
@SETF(FLG_STEP_ONET)
@SETF(FLG_BOX_ONET_NES_NES_BAT)	;バットは取ってる
@MOVE_GOM_CHAR( GOM_HIT_CHR,ANIM_INTO_NIGHT,OBJFX_SHOW_NONE )	;フェードアウト
@WAITSYS()
@WI(1)
@HIDE_PLAYER( PC_ALL, OBJFX_HIDE_NONE )		;パーティ消す
;真っ暗画面へワープ(暗闇でポーキーの「扉どんどん」BGMを鳴らすため)
@WARP( WARP_PUBLIC_BLACKSCREEN )
@WI( 180 )
;ネスの寝室へワープ
@MOVE_GOM_CHAR( GOM_NES_PAZYAMA, ANIM_SEAL_PC_ALL_DIRLEFT, OBJFX_AFTER_WARP )
@WARP(WARP_MYHOME_2ND);パジャマに着替えてベッドから
@COLD_CHAR( PC_ALL )
@WI( 1 )
@HIDE_GOM_CHAR( GOM_NES_PAZYAMA, OBJFX_HIDE_NONE )
@SHOW_PLAYER( PC_ALL, OBJFX_SHOW_NONE )		;パーティ表示
@WI(1)
@SETF(FLG_SYS_DIS_2H_PAPA)	;二時間パパ禁止
@SETF(FLG_SYS_MONSTER_OFF)	;ワンダリングモンスター禁止(念のため)
』

MSG_EVT_KNOCK_TO_BE;続く!イベント２
@BGMSTOP(0)
@SE(SE_se08_door_in)
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_NESS_TOBINOKU,OBJFX_SHOW_NONE)
@GOSUB( MSG_FOLLOW_GOM_HIT_CHR_SUBR )	;透明君について行く
;
;@MOVE_GOM_CHAR(GOM_PICKEY,ANIM_TO_BE_PICKEY,OBJFX_SHOW_NONE);ピッキー登場
@MOVE_GOM_CHAR(GOM_PICKEY,ANIM_PEOPLEWALK0_A,OBJFX_SHOW_NONE);ピッキー登場
@HIDE_CHAR(PRSN_ONET_NES_1F_3B,OBJFX_HIDE_NONE);ノックを消す
@SE(SE_se09_door_out)
@SET_CAMERA_PARTY()
@GET_GOM_CHAR_DIR(GOM_PICKEY,1,PC_NESS);ネスの方を向く
@SET_GOM_CHAR_DIR(GOM_PICKEY,0)
@GET_PLAYER_DIR(PC_NESS,3,GOM_PICKEY);ネスからピッキーの方向を調べる
@SET_PLAYER_DIR(PC_NESS,0);ネスをその方向に向ける
@COLD_PLAYER(PC_ALL)
@WI(1)
@COLD_GOM_CHAR(GOM_PICKEY)
@CLRF(FLG_MYHOME_KNOCK_APPEAR)
@SETF(FLG_MYHOME_DOOR_CLOSE)
@SETF(FLG_MYHOME_POKEY_ENTER);曲設定用
;
@WI(30)
@BGMSTART(0,177);poky0
@WI(120)@WI(120)
;@MUSISTART();ファンファーレ終了で曲終わりにする。
@OPEN(1)
@WI(20)
◆A letter arrived from my brother, Pokey...it is addressed to @DSP_NAME(PC_NESS).@KEY()↓
◆There’s no stamp, and it’s not time for the mailman to come...@KEY()↓
◆Anyway, I brought it over.@KEY()↓
◆It says, ”come and get me, loser!@KEY()↓
◆Spankety, spankety, spankety.”@KEY()↓
◆...I wonder where he is?
;jpn:◆ポーキーにいちゃんから↓
;jpn: @DSP_STS(STS_PC_NAME_NES)あての↓
;jpn: てがみが とどいていたんだ!@KEY()↓
;jpn:◆スタンプも ないし↓
;jpn: ゆうびんやさんが くるような↓
;jpn: じかんじゃ ないし‥‥@KEY()↓
;jpn:◆でも とにかく↓
;jpn: もってきたよ。@KEY()↓
;jpn:◆「ここまで おいで!@WI(20)↓
;jpn: おしりペンペーン」@WI(30)‥‥だって。@KEY()↓
;jpn:◆‥‥どこにいるんだろう?@KEYNP()@WI(20)
;◆とつぜん トカゲが↓
; たずねてきて@KEY()↓
;◆ポーキーにいちゃんの↓
; てがみを おいてったんだ。@KEY()↓
;◆「ここまで おいで!@WI(20)↓
; おしりペンペーン」@WI(30)‥‥だって。@KEY()↓
;◆‥‥どこにいるんだろう?
@KEYNP()@WI(20)
@CLOSEALL()
@WI(1)
@SETF(FLG_TLPT_ONET)			;曲設定用(無音化)
;楕円ウィンドウ閉じる
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_TOBECONT_OVALCLOSE,OBJFX_SHOW_NONE)
@WAITSYS()
@HIDE_PLAYER(PC_ALL,OBJFX_HIDE_NONE)
@WARP( WARP_PUBLIC_BLACKSCREEN )	;真っ黒画面へワープ
@MOVE_GOM_CHAR(GOM_HIT_CHR,ANIM_TOBECONT_TITLE,OBJFX_SHOW_NONE)
@WAITSYS()
_ROOP_TO_BE;永久ループ
@WI(1)
@GOTO(_ROOP_TO_BE)
;-------------------------------------------------
